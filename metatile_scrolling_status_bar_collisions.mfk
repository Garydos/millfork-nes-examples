// compile with -t nes_small_v

//use the dpad to move the player around

import nes_joy

const word ppu_nametable0 = $2000
const word ppu_nametable1 = $2400
const word ppu_nametable2 = $2800
const word ppu_nametable3 = $2C00
const word ppu_attrtable0 = $23C0
const word ppu_attrtable1 = $27C0
const word ppu_attrtable2 = $2BC0
const word ppu_attrtable3 = $2FC0
const word ppu_pallete_ram = $3F00

array oam_buffer [256] @$200 // sprite buffer

//constant offsets for oam_buffer
//so we can draw the player metasprite easily,
//in a real game you should use a oam manager
const byte playersprite0_x = 4
const byte playersprite0_tile = 5
const byte playersprite0_attrs = 6
const byte playersprite0_y = 7

const byte playersprite1_x = 8
const byte playersprite1_tile = 9
const byte playersprite1_attrs = 10
const byte playersprite1_y = 11

const byte playersprite2_x = 12
const byte playersprite2_tile = 13
const byte playersprite2_attrs = 14
const byte playersprite2_y = 15

const byte playersprite3_x = 16
const byte playersprite3_tile = 17
const byte playersprite3_attrs = 18
const byte playersprite3_y = 19

//player sprite constants
const byte sprite0_attrs = 0
const byte sprite1_attrs = 0
const byte sprite2_attrs = 0
const byte sprite3_attrs = 0
const byte sprite0_tile = 0
const byte sprite1_tile = 1
const byte sprite2_tile = 2
const byte sprite3_tile = 3

//collision flags
bool left_collis_flag
bool right_collis_flag
bool up_collis_flag
bool down_collis_flag

byte playerx
byte playery
sbyte playerxvel
sbyte playeryvel
const byte player_width = 12 //width in pixels
const byte player_height = 12 //height in pixels

byte nametable //current nametable
byte draw_seam_column //column at the draw/scroll seam
byte next_column      //column to be drawn to next
byte current_screen      //screen to load next
byte xscroll          //screen x scroll value
bool scrolling_right
bool scrolling_left
byte previous_scroll //0 = scrolling right, 1 = scrolling left
byte draw_counter

//variables for debugging, find these
//in fceux's hex editor and watch them
byte _current_tile //the current tile player position is touching

void main() {
    init_sprites()
    load_palletes()
    clear_status_bar()
    draw_status_bar()
    prepare_sprite0()
    
    xscroll = 0
    nametable = 0
    draw_seam_column = 0
    draw_counter = 0
    previous_scroll = 0
    current_screen = 0
    playerx = 10
    playery = $60
    scrolling_right = false
    scrolling_left = false
    left_collis_flag = false
    right_collis_flag = false
    up_collis_flag = false
    down_collis_flag = false

    
    //screen 1
    draw_full_screen(0,0)
    load_attr_column(0,0,0)
    load_attr_column(1,0,1)
    load_attr_column(2,0,2)
    load_attr_column(3,0,3)
    load_attr_column(4,0,4)
    load_attr_column(5,0,5)
    load_attr_column(6,0,6)
    load_attr_column(7,0,7)
    
    ppu_set_scroll(0,0)
    ppu_wait_vblank()
    ppu_ctrl = %10010000   // enable NMI, sprites from Pattern Table 0, background from Pattern Table 1
    ppu_mask = %00011110   // enable sprites, enable background, no clipping on left side

    while(true) {}
}

void nmi() {
    ppu_oam_dma_write(oam_buffer.addr.hi)
    
    //either scrolling right or have a column to finish drawing
    if scrolling_right || (previous_scroll == 0 && draw_counter > 0) {
        
        right_metatile_scroll()
        
        //update the scroll variable
        if scrolling_right {
            update_scroll_right()
        }
        
        previous_scroll = 0
    }
    //either scrolling left or have a column to finish drawing
    else if scrolling_left || (previous_scroll == 1 && draw_counter > 0) {
        //left scroll logic
        
        if scrolling_left {
            update_scroll_left()
        }
        
        left_metatile_scroll()
        
        previous_scroll = 1
    }
    
    //set scroll to 0 for now so that the status bar
    //stays still
    ppu_set_scroll(0,0)
    ppu_ctrl = %10010000 // no need to or the nametable for this one,
                         // as the status bar should always be at the first nametable
    ppu_mask = %00011100
    
    //now we start checking for a sprite0 hit
    //so that we know the status bar is done drawing
    
    //first wait for the previous frame's sprite0 hit
    //to clear
    while (ppu_status & %01000000) != 0 {
    }
    
    //then wait for this frame's sprite0 hit
    while (ppu_status & %01000000) == 0 {
    }
    
    //now wait for the end of the scanline
    wait_cycles($10)
    
    //we have now reached the end of the scanline, and the
    //status bar is done drawing, so draw the rest of
    //the screen with the correct scroll values
    ppu_set_scroll(xscroll,0)
    ppu_ctrl = %10010000 | nametable
    ppu_mask = %00011100

    //game logic goes here
    read_joy1()
    if input_dx > 0 {
        // scrolling_right = true
        // scrolling_left = false
        if playerx < $F0 {
            playerxvel += 2
        }
        if playerx >= $78 && not(current_screen == 3 && xscroll == 0) {
            //scroll the screen as well
            scrolling_right = true
            scrolling_left = false
        }
        else {
            scrolling_left = false
            scrolling_right = false
        }
    }
    else if input_dx < 0 {
        // scrolling_right = false
        // scrolling_left = true
        if playerx > $08 {
            playerxvel -= 2
        }
        if playerx <= $78 && not(current_screen == 0 && xscroll == 0) {
            //scroll the screen as well
            scrolling_right = false
            scrolling_left = true
        }
        else {
            scrolling_left = false
            scrolling_right = false
        }
    }
    else {
        scrolling_left = false
        scrolling_right = false
    }
    
    if input_dy > 0 {
        // scrolling_right = true
        // scrolling_left = false
        if playery < $D0 {
            playeryvel += 2
        }
    }
    else if input_dy < 0 {
        // scrolling_right = false
        // scrolling_left = true
        if playery > $20 {
            playeryvel -= 2
        }
    }
    
    check_player_collis_and_update_player_loc()
    if scrolling_right {
        //update the position of the screen sprites
        playerx -= 2
    }
    else if scrolling_left {
        //update the position of the screen sprites
        playerx += 2
    }
    snap_player_pos_limits()
    update_player_sprites()
    
    //reset velocity
    playerxvel = 0
    playeryvel = 0
}

void irq() {
}

void check_player_collis_and_update_player_loc() {
    byte old_playerx
    byte old_playery
    
    old_playerx = playerx
    old_playery = playery
    
    //do vertical collision checking
    playery += playeryvel
    
    check_background_collis(playerx, playery, player_width, player_height)
    if up_collis_flag || down_collis_flag || left_collis_flag || right_collis_flag{
        playery = old_playery
    }
    reset_collis_flags()
    
    //do horizontal collision checking
    playerx += playerxvel
    
    check_background_collis(playerx, playery, player_width, player_height)
    if up_collis_flag || down_collis_flag || left_collis_flag || right_collis_flag{
        playerx = old_playerx
        scrolling_left = false
        scrolling_right = false
    }
    reset_collis_flags()
}

inline void snap_player_pos_limits() {
    //snap the player back to the playable area of the screen
    if playerx > $F0 {
        playerx = $F0
    }
    if playerx < $08 {
        playerx = $08
    }
    if playery > $D0 {
        playery = $D0
    }
    if playery < $20 {
        playery = $20 
    }
}

inline void reset_collis_flags() {
    left_collis_flag = false
    right_collis_flag = false
    up_collis_flag = false
    down_collis_flag = false
}

byte get_column_from_screenx(byte screenx) {
    byte new_column
    word new_column_offset //must be a word because (xscroll+newplayerx) can overflow
    
    new_column = current_screen * $20 //leftmost column of current screen
    new_column_offset = xscroll
    new_column_offset += screenx
    new_column_offset /= 8
    new_column += new_column_offset.lo
    if new_column >= $80 {
       new_column -= $80
    }
    
    return new_column
}

inline byte get_row_from_screeny(byte screeny) {
    return screeny/8
}

void check_background_collis(byte screenx, byte screeny, byte spritewidth, byte spriteheight) {
    //check if the bounding box is colliding with collidable background tiles
    //on its left and right edges
    byte new_column1
    byte new_column2
    byte new_row1
    byte new_row2
    byte i
    
    //do the collision stuff, set the left and right
    //flags depending on left/right or up/down collisions
    //assume that any tile that is not $24 (sky tile) is collidable

    //LEFT SIDE
    new_column1 = get_column_from_screenx(screenx)
    new_row1 = get_row_from_screeny(screeny)
    new_row2 = get_row_from_screeny(screeny+spriteheight)
    
    for i,0,to,new_row2-new_row1{
        _current_tile = get_tile(new_column1, new_row1+i)
        if _current_tile != $24 {
            left_collis_flag = true
        }
    }
    
    //RIGHT SIDE
    new_column1 = get_column_from_screenx(screenx+spritewidth)
    new_row1 = get_row_from_screeny(screeny)
    new_row2 = get_row_from_screeny(screeny+spriteheight)
    
    for i,0,to,new_row2-new_row1{
        _current_tile = get_tile(new_column1, new_row1+i)
        if _current_tile != $24 {
            right_collis_flag = true
        }
    }
    
    //UP SIDE
    new_row1 = get_row_from_screeny(screeny)
    new_column1 = get_column_from_screenx(screenx)
    new_column2 = get_column_from_screenx(screenx+spritewidth)
    
    for i,0,to,new_column2-new_column1{
        _current_tile = get_tile(new_column1+i, new_row1)
        if _current_tile != $24 {
            up_collis_flag = true
        }
    }
    
    //DOWN SIDE
    new_row1 = get_row_from_screeny(screeny+spritewidth)
    new_column1 = get_column_from_screenx(screenx)
    new_column2 = get_column_from_screenx(screenx+spritewidth)
    
    for i,0,to,new_column2-new_column1{
        _current_tile = get_tile(new_column1+i, new_row1)
        if _current_tile != $24 {
            down_collis_flag = true
        }
    }
}

inline void update_player_speed() {
    playerx += playerxvel
    playery += playeryvel
}

inline void update_scroll_left() {
    xscroll -= 2
    if xscroll >= $FE {
        current_screen -= 1
        if current_screen >= 4 {
            current_screen = 3
        }
        nametable ^= %00000001
    }
    if (xscroll+2) & %00000111 == 0 {
        draw_seam_column = current_screen * $20
        draw_seam_column += xscroll/8
    }
}

inline void left_metatile_scroll() {
    if draw_counter == 0 && (xscroll & %00011111 == 0 || xscroll == $FE){
        // about to enter a 32 pixel boundary, so
        // draw the attributes
        next_column = draw_seam_column
        
        if xscroll == $FE {
            //if xscroll = $FE then that means we haven't moved the screen yet,
            //so the next column still needs to be drawn
            draw_counter = 1
            load_attr_column($07,nametable,next_column/4)
        }
        else if (xscroll != 0){
            //if xscroll == 0 then that means loading attributes will be taken
            //care of by xscroll == $FE next scroll, so skip it
            load_attr_column(xscroll/32 - 1,nametable,next_column/4 - 1)
            //we subtract 1 from the 1st and 3rd arguments because we're loading
            //in attribute tables 1 step ahead in order to keep up with scrolling
        }
    }
    else if (xscroll+2) & %00001111 == 0 || draw_counter > 0 {
        // on a 16 pixel boundary, so
        // load the appropriate tiles
        next_column = draw_seam_column

        draw_column_with_counter(xscroll/16,nametable,next_column/2)
    }
}

inline void update_scroll_right() {
    xscroll += 2
    if xscroll == 0 {
        current_screen += 1
        if current_screen >= 4 {
            current_screen = 0
        }
        nametable ^= %00000001
    }
    if xscroll & %00000111 == 0 {
        draw_seam_column = current_screen * $20
        draw_seam_column += xscroll/8
    }
}

inline void right_metatile_scroll() {
    // use an else-if here
    // so that we don't load attrs and tiles
    // on the same frame, as that would take
    // too much time and bleed into screen rendering
    if draw_counter == 0 && ((xscroll+2) & %00011111 == 0 || xscroll == 0){
        // about to enter a 32 pixel boundary, so
        // draw the attributes
        next_column = draw_seam_column + $20
        if next_column >= $80 {
            // if we're on the final screen, then wrap around to the beginning
            next_column -= $80
        }
        
        if xscroll == 0 {
            //if xscroll = 0 then that means we haven't moved the screen yet,
            //so the next column still needs to be drawn
            draw_counter = 1
            load_attr_column($00,nametable ^ %00000001,next_column/4)
        }
        else if (xscroll+2) != 0 {
            //(xcroll+2) == 0 is taken care of by xscroll==0
            load_attr_column((xscroll+2)/32,nametable ^ %00000001,next_column/4 + 1)
            //add 1 to 3rd argument because we are loading the column that is one ahead
            //of our loading seam
        }
        
    }
    else if (xscroll & %00001111) == 0 || draw_counter > 0 {
        // on a 16 pixel boundary, so
        // load the appropriate tiles
        next_column = draw_seam_column + $20
        
        if next_column >= $80 {
            // if we're on the final screen, then wrap around to the beginning
            next_column -= $80
        }
        draw_column_with_counter(xscroll/16,nametable ^ %00000001,next_column/2)
    }
}

void draw_status_bar() {
    //draw the status bar accross both nametables
    byte i
    
    //set ppu address increment to 1 so we can draw the left and right borders
    //(allows us to draw to the nametable in vertical strips rather than horizontal)
    ppu_ctrl = %00000000
    read_ppu_status()
    
    //write to first nametable
    ppu_set_addr(ppu_nametable0+$40)
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($11) //$11 = 'H'
    ppu_write_data($0E) //$0E = 'E'
    ppu_write_data($0A) //$0A = 'A'
    ppu_write_data($15) //$15 = 'L'
    ppu_write_data($1D) //$1D = 'T'
    ppu_write_data($11) //$11 = 'H'
    ppu_write_data($25) //$25 = colon
    ppu_write_data($34) //$34 = full box
    ppu_write_data($34) //$34 = full box
    ppu_write_data($35) //$35 = empty box
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($26) //$26 = lives icon
    ppu_write_data($27) //$26 = multiply icon
    ppu_write_data($03) //$03 = 3
    
    //write to second nametable
    ppu_set_addr(ppu_nametable1+$40)
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($11) //$11 = 'H'
    ppu_write_data($0E) //$0E = 'E'
    ppu_write_data($0A) //$0A = 'A'
    ppu_write_data($15) //$15 = 'L'
    ppu_write_data($1D) //$1D = 'T'
    ppu_write_data($11) //$11 = 'H'
    ppu_write_data($25) //$25 = colon
    ppu_write_data($34) //$34 = full box
    ppu_write_data($34) //$34 = full box
    ppu_write_data($35) //$35 = empty box
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($24) //$24 = sky tile
    ppu_write_data($26) //$26 = lives icon
    ppu_write_data($27) //$26 = multiply icon
    ppu_write_data($03) //$03 = 3
    
    
}

void clear_status_bar() {
    //clear the status bar to all sky tiles
    byte i
    
    //set ppu address increment to 1 so we can draw the left and right borders
    //(allows us to draw to the nametable in vertical strips rather than horizontal)
    ppu_ctrl = %00000000
    read_ppu_status()
    
    ppu_set_addr(ppu_nametable0)
    for i,0,until,$80 {
        ppu_write_data($24) //$24 = sky tile
    }
    
    ppu_set_addr(ppu_nametable1)
    for i,0,until,$80 {
        ppu_write_data($24) //$24 = sky tile
    }
}

void load_attr_column(byte screen_column, byte nametable, byte attrs_column) {
    byte i
    pointer attrs_ptr
    attrs_ptr = get_attrs_column(attrs_column)

    //set ppu address increment to 1 so we can draw the left and right borders
    //(allows us to draw to the nametable in vertical strips rather than horizontal)
    ppu_ctrl = %00000000

    read_ppu_status()
    for i,0,until,$08 {
        if nametable == 0 {
            ppu_set_addr(ppu_attrtable0 + i*8 + screen_column)
        }
        else if nametable == 1 {
            ppu_set_addr(ppu_attrtable1 + i*8 + screen_column)
        }
        else if nametable == 2 {
            ppu_set_addr(ppu_attrtable2 + i*8 + screen_column)
        }
        else if nametable == 3 {
            ppu_set_addr(ppu_attrtable3 + i*8 + screen_column)
        }

        ppu_write_data(attrs_ptr[i])
    }
}

inline pointer get_attrs_column(byte metatiles_column) {
    pointer attrs_ptr
    attrs_ptr = metatiles_column
    attrs_ptr *= $08
    attrs_ptr += attrs
    return attrs_ptr
}

void draw_full_screen(byte nametable, byte metatile_column) {
    draw_column(0,nametable,metatile_column)
    metatile_column += 1
    draw_column(1,nametable,metatile_column)
    metatile_column += 1
    draw_column(2,nametable,metatile_column)
    metatile_column += 1
    draw_column(3,nametable,metatile_column)
    metatile_column += 1
    draw_column(4,nametable,metatile_column)
    metatile_column += 1
    draw_column(5,nametable,metatile_column)
    metatile_column += 1
    draw_column(6,nametable,metatile_column)
    metatile_column += 1
    draw_column(7,nametable,metatile_column)
    metatile_column += 1
    draw_column(8,nametable,metatile_column)
    metatile_column += 1
    draw_column(9,nametable,metatile_column)
    metatile_column += 1
    draw_column(10,nametable,metatile_column)
    metatile_column += 1
    draw_column(11,nametable,metatile_column)
    metatile_column += 1
    draw_column(12,nametable,metatile_column)
    metatile_column += 1
    draw_column(13,nametable,metatile_column)
    metatile_column += 1
    draw_column(14,nametable,metatile_column)
    metatile_column += 1
    draw_column(15,nametable,metatile_column)
}

void draw_column_with_counter(byte screen_column, byte nametable, byte metatiles_column) {
    pointer metatiles_ptr
    
    screen_column *= 2 //convert to metatile column #
    metatiles_ptr = get_metatile_column(metatiles_column)
    
    //set ppu address increment to 32 so we can draw the left and right borders
    //(allows us to draw to the nametable in vertical strips rather than horizontal)
    ppu_ctrl = %00000100
    read_ppu_status()
    
    if draw_counter == 0 || draw_counter == 1 {
        //draw the first column on the first frame
        draw_column_first_draw(nametable, screen_column, metatiles_ptr)
        draw_counter = 2
    }
    else if draw_counter == 2 {
        //draw the second column on the second frame
        draw_column_second_draw(nametable, screen_column, metatiles_ptr)
        draw_counter = 0
    }
}

inline void draw_column_first_draw(byte nametable, byte screen_column, pointer metatiles_ptr) {
    //first frame of the column draw
    //draws the leftmost column of the metatile column
    byte i
    
    if nametable == 0 {
        ppu_set_addr(ppu_nametable0 + $80 + screen_column)
    }
    else if nametable == 1 {
        ppu_set_addr(ppu_nametable1 + $80 + screen_column)
    }
    else if nametable == 2 {
        ppu_set_addr(ppu_nametable2 + $80 + screen_column)
    }
    else if nametable == 3 {
        ppu_set_addr(ppu_nametable3 + $80 + screen_column)
    }

    //skip to $02 because the metatile data is structured to ignore the
    //first two metatiles, and end before $0F because that byte is a junk
    //byte as well
    for i,$02,until,$0F {
        draw_metatile_first_column(metatiles_ptr[i])
    }
}

inline void draw_column_second_draw(byte nametable, byte screen_column, pointer metatiles_ptr) {
    //second frame of the column draw
    //draws the rightmost column of the metatile column
    byte i
    
    if nametable == 0 {
        ppu_set_addr(ppu_nametable0 + $80 + screen_column + 1)
    }
    else if nametable == 1 {
        ppu_set_addr(ppu_nametable1 + $80 + screen_column + 1)
    }
    else if nametable == 2 {
        ppu_set_addr(ppu_nametable2 + $80 + screen_column + 1)
    }
    else if nametable == 3 {
        ppu_set_addr(ppu_nametable3 + $80 + screen_column + 1)
    }
    
    //skip to $02 because the metatile data is structured to ignore the
    //first two metatiles, and end before $0F because that byte is a junk
    //byte as well
    for i,$02,until,$0F {
        draw_metatile_second_column(metatiles_ptr[i])
    }
}

void draw_column(byte screen_column, byte nametable, byte metatiles_column) {
    byte i
    pointer metatiles_ptr
    
    screen_column *= 2
    metatiles_ptr = get_metatile_column(metatiles_column)
    
    //set ppu address increment to 32 so we can draw the left and right borders
    //(allows us to draw to the nametable in vertical strips rather than horizontal)
    ppu_ctrl = %00000100
    read_ppu_status()
    
    if nametable == 0 {
        ppu_set_addr(ppu_nametable0 + $80 + screen_column)
    }
    else if nametable == 1 {
        ppu_set_addr(ppu_nametable1 + $80 + screen_column)
    }
    else if nametable == 2 {
        ppu_set_addr(ppu_nametable2 + $80 + screen_column)
    }
    else if nametable == 3 {
        ppu_set_addr(ppu_nametable3 + $80 + screen_column)
    }

    //skip to $02 because the metatile data is structured to ignore the
    //first two metatiles, and end before $0F because that byte is a junk
    //byte as well
    for i,$02,until,$0F {
        draw_metatile_first_column(metatiles_ptr[i])
    }
    
    
    if nametable == 0 {
        ppu_set_addr(ppu_nametable0 + $80 + screen_column + 1)
    }
    else if nametable == 1 {
        ppu_set_addr(ppu_nametable1 + $80 + screen_column + 1)
    }
    else if nametable == 2 {
        ppu_set_addr(ppu_nametable2 + $80 + screen_column + 1)
    }
    else if nametable == 3 {
        ppu_set_addr(ppu_nametable3 + $80 + screen_column + 1)
    }
    
    //skip to $02 because the metatile data is structured to ignore the
    //first two metatiles
    for i,$02,until,$0F {
        draw_metatile_second_column(metatiles_ptr[i])
    }
}

inline pointer get_metatile_column(byte metatiles_column) {
    pointer metatiles_ptr
    metatiles_ptr = metatiles_column
    metatiles_ptr *= $10
    metatiles_ptr += screen_metatiles
    return metatiles_ptr
}

inline void draw_metatile_first_column(byte metatile) {
    pointer metatile_ptr
    metatile_ptr = metatiles
    metatile_ptr += metatile*4
    
    ppu_write_data(metatile_ptr[0])
    ppu_write_data(metatile_ptr[1])
}

inline void draw_metatile_second_column(byte metatile) {
    pointer metatile_ptr
    metatile_ptr = metatiles
    metatile_ptr += metatile*4
    
    ppu_write_data(metatile_ptr[2])
    ppu_write_data(metatile_ptr[3])
}

byte get_tile(byte column, byte row) {
    //return the tile number of the tile
    //located at column by row
    //here, column refers to the 8 pixel wide
    //screen columns, not the 16 pixel wide
    //metatile columns
    byte metatile_column_num
    byte metatile_row_num
    byte tile
    pointer metatiles_column
    pointer metatile_ptr
    
    //get the metatile row and column numbers
    metatile_column_num = column / 2
    metatile_row_num = row / 2
    
    //get the metatile column data pointer
    metatiles_column = get_metatile_column(metatile_column_num)
    //store the metatile tile in the tile variable in preparation
    //for finding the proper screen tile number
    tile = metatiles_column[metatile_row_num]
    
    //now we can use the original column and row numbers to
    //get the proper tile
    metatile_ptr = metatiles
    metatile_ptr += tile*4
    
    //convert column and row to metatile definition offsets
    column &= %00000001 //column now either 0 or 1 depending on odd or even
    column <<= 1        //column now either 0 or 2
    row &= %00000001    //row now either 0 or 1
    
    //get the proper tile from the new indices
    tile = metatile_ptr[column + row]
    
    return tile
}

void prepare_sprite0() {
    //move sprite0 to overlap with bottom edge
    //of the status bar in preperation for splitting
    //the screen
    oam_buffer[0] = $16
    oam_buffer[1] = $FF
    oam_buffer[2] = $00
    oam_buffer[3] = $08
}

void init_sprites() {
    byte i
    for i,0,to,255 {
        if (i & %00000011) == 0 {
            //each sprite takes up 4 bytes, and we want to edit
            //the y position of each sprite (0th byte)
            //so we use the %00000011 mask to write every 4th byte (every 0th sprite byte)
            
            oam_buffer[i] = $ef // move the sprite off screen
        }
        else {
            oam_buffer[i] = 0
        }
    }
}

void load_palletes() {
    byte i
    read_ppu_status() // read PPU status to reset the high/low latch
    ppu_set_addr(ppu_pallete_ram) // point the PPU to palette ram
    for i,0,until,$20 {
        ppu_write_data(pallete[i])
    }
}

void update_player_sprites() {
    oam_buffer[playersprite0_x] = playery
    oam_buffer[playersprite0_tile] = sprite0_tile
    oam_buffer[playersprite0_attrs] = sprite0_attrs
    oam_buffer[playersprite0_y] = playerx
    
    oam_buffer[playersprite1_x] = playery
    oam_buffer[playersprite1_tile] = sprite1_tile
    oam_buffer[playersprite1_attrs] = sprite1_attrs
    oam_buffer[playersprite1_y] = playerx + 8
    
    oam_buffer[playersprite2_x] = playery + 8
    oam_buffer[playersprite2_tile] = sprite2_tile
    oam_buffer[playersprite2_attrs] = sprite2_attrs
    oam_buffer[playersprite2_y] = playerx
    
    oam_buffer[playersprite3_x] = playery + 8
    oam_buffer[playersprite3_tile] = sprite3_tile
    oam_buffer[playersprite3_attrs] = sprite3_attrs
    oam_buffer[playersprite3_y] = playerx + 8
}

inline asm void ppu_wait_vblank() {
    vblankwait:
        BIT $2002
        ! BPL vblankwait
    ? RTS
}

inline asm void wait_cycles(byte x) {
    //burn some cycles to wait for the end of the scanline
    WaitScanline:
        DEX
        ! BNE WaitScanline
    ? RTS
}

const array pallete = [
  $0F,$17,$18,$29,$34,$38,$18,$2A,$38,$39,$3A,$3B,$3C,$3D,$3E,$0F, //nametable palettes
  $3C,$10,$37,$0F,$07,$10,$2D,$0F,$0F,$00,$0F,$36,$31,$02,$38,$3C  //sprite palettes
]

//define each metatile
const array metatiles = [
			//Tile positions:
			//top-left	bot-left	top-right	bot-right
            $24,		$24,		$24,		$24, // 		$00, should always be
                                                     //        all background
            $30,		$31,		$30,		$31, // 		$01, basic top ground tile
            $31,		$31,		$31,		$31, //		$02, all ground tile
            $24,		$32,		$24,		$24, //		$03, floor flower tile
            $24,		$24,		$24,		$33, //		$04, floor grass tile
            $24,		$36,		$24,		$37, //		$05, bushes tile A
            $24,		$30,		$24,		$24 //		$06, single-tile platform
]


//contains 4 screens worth of metatiles
//each metatile column is 16 bytes long
//each metatile column contains 3 bytes of wasted space, two at the top, and one at end
//(this is a holdover from one of my older projects, you could rearange it so that all
//the unused bytes are contiguous, you'd just have to change the loops in the draw_column functions)
const array screen_metatiles = [
	//note: all columns will have 3 wasted bytes
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF, 		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF, 		// 15 meta tiles to fill a column plus one filler tile

	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $03, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $04, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $06, $00, $00, $00, $00,
		$00, $00, $00, $00, $05, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $01, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $00, $00, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $01, $00, $00, $00,
		$00, $00, $00, $00, $00, $00, $00, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $01, $00, $00, $00,
		$00, $00, $00, $00, $00, $00, $00, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $01, $00, $00, $00,
		$00, $00, $00, $00, $03, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $01, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile

	 $00, $00, $00, $00, $01, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $01, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $01, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $01, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile

	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $03, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $04, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile

	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $01, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $01, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $01, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $01, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $00, $00, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $00, $00, $FF,		// 15 meta tiles to fill a column plus one filler tile

	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $01, $00, $00, $00, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $01, $00, $00, $00, $FF, 		// 15 meta tiles to fill a column plus one filler tile
        
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,     // 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile

	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile

	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $03, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $04, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile

	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF,		// 15 meta tiles to fill a column plus one filler tile
	
	 $00, $00, $00, $00, $00, $00, $00, $00,
		$00, $00, $00, $00, $00, $01, $02, $FF 		// 15 meta tiles to fill a column plus one filler tile

]

const array attrs = file("attrib.bin")


segment(chrrom) const array graphics @ $0000 = file("nesthing.chr")